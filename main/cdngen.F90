!--------------------------------------------------------------------------------
! Copyright (c) 2016 Peter Grünberg Institut, Forschungszentrum Jülich, Germany
! This file is part of FLEUR and available as free software under the conditions
! of the MIT license as expressed in the LICENSE file in more detail.
!--------------------------------------------------------------------------------
MODULE m_cdngen
CONTAINS

SUBROUTINE cdngen(eig_id,mpi,input,banddos,sliceplot,vacuum,&
                  kpts,atoms,sphhar,stars,sym,gfinp,hub1inp,&
                  enpara,cell,noco,nococonv,vTot,results,oneD,coreSpecInput,&
                  archiveType, xcpot,outDen,EnergyDen,greensFunction,hub1data)

   !*****************************************************
   !    Charge density generator
   !    calls cdnval to generate the valence charge and the
   !    core routines for the core contribution
   !*****************************************************

   USE m_types
   USE m_constants
   USE m_juDFT
   USE m_prpqfftmap
   USE m_cdnval
   USE m_plot
   USE m_cdn_io
   USE m_wrtdop
   USE m_cdntot
   USE m_qfix
   USE m_genNewNocoInp
   USE m_xmlOutput
   USE m_magMoms
   USE m_magMultipoles
   USE m_orbMagMoms
   USE m_resMoms
   USE m_cdncore
   USE m_doswrite
   USE m_Ekwritesl
   USE m_banddos_io
   USE m_metagga
   USE m_unfold_band_kpts
   USE m_denMultipoleExp
   USE m_greensfPostProcess
   USE m_angles
#ifdef CPP_MPI
   USE m_mpi_bc_potden
#endif

   IMPLICIT NONE

#ifdef CPP_MPI
   INCLUDE 'mpif.h'
#endif

   ! Type instance arguments
   TYPE(t_results),INTENT(INOUT)    :: results
   TYPE(t_mpi),INTENT(IN)           :: mpi

   TYPE(t_oneD),INTENT(IN)          :: oneD
   TYPE(t_enpara),INTENT(INOUT)     :: enpara
   TYPE(t_banddos),INTENT(IN)       :: banddos
   TYPE(t_sliceplot),INTENT(IN)     :: sliceplot
   TYPE(t_input),INTENT(IN)         :: input
   TYPE(t_vacuum),INTENT(IN)        :: vacuum
   TYPE(t_noco),INTENT(IN)          :: noco
   TYPE(t_nococonv),INTENT(INOUT)   :: nococonv
   TYPE(t_sym),INTENT(IN)           :: sym
   TYPE(t_stars),INTENT(IN)         :: stars
   TYPE(t_cell),INTENT(IN)          :: cell
   TYPE(t_kpts),INTENT(IN)          :: kpts
   TYPE(t_sphhar),INTENT(IN)        :: sphhar
   TYPE(t_atoms),INTENT(IN)         :: atoms
   TYPE(t_coreSpecInput),INTENT(IN) :: coreSpecInput
   TYPE(t_potden),INTENT(IN)        :: vTot
   TYPE(t_gfinp),INTENT(IN)         :: gfinp
   TYPE(t_hub1inp),INTENT(IN)       :: hub1inp
   TYPE(t_greensf),OPTIONAL,INTENT(INOUT)    :: greensFunction(:)
   TYPE(t_hub1data),OPTIONAL,INTENT(INOUT)    :: hub1data
   CLASS(t_xcpot),INTENT(IN)     :: xcpot
   TYPE(t_potden),INTENT(INOUT)     :: outDen, EnergyDen

   !Scalar Arguments
   INTEGER, INTENT (IN)             :: eig_id, archiveType

   ! Local type instances
   TYPE(t_noco)            :: noco_new
   TYPE(t_regionCharges)   :: regCharges, fake_regCharges
   TYPE(t_dos)             :: dos, fake_dos
   TYPE(t_moments)         :: moments, fake_moments
   TYPE(t_results)         :: fake_results
   TYPE(t_mcd)             :: mcd
   TYPE(t_slab)            :: slab
   TYPE(t_orbcomp)         :: orbcomp
   TYPE(t_jDOS)            :: jDOS
   TYPE(t_cdnvalJob)       :: cdnvalJob
   TYPE(t_greensfImagPart) :: greensfImagPart
   TYPE(t_potden)          :: val_den, core_den


   !Local Scalars
   REAL                  :: fix, qtot, dummy,eFermiPrev
   INTEGER               :: jspin, jspmax, ierr
   INTEGER               :: dim_idx
   INTEGER               :: i_gf,iContour

#ifdef CPP_HDF
   INTEGER(HID_T)        :: banddosFile_id
#endif
   LOGICAL               :: l_error, perform_MetaGGA

   CALL regCharges%init(input,atoms)
   CALL dos%init(input,atoms,kpts,vacuum)
   CALL moments%init(mpi,input,sphhar,atoms)
   CALL mcd%init1(banddos,input,atoms,kpts)
   CALL slab%init(banddos,atoms,cell,input,kpts)
   CALL orbcomp%init(input,banddos,atoms,kpts)
   CALL jDOS%init(input,banddos,atoms,kpts)

   IF(PRESENT(greensFunction).AND.gfinp%n.GT.0) THEN
      !Only calculate the greens function when needed
      DO i_gf = 1, gfinp%n
         IF(.NOT.greensFunction(i_gf)%l_calc) CYCLE
         iContour = gfinp%elem(i_gf)%iContour
         CALL greensFunction(i_gf)%contour%eContour(gfinp%contour(iContour),results%ef,mpi%irank)
         CALL greensFunction(i_gf)%reset()
      ENDDO
      CALL greensfImagPart%init(gfinp,input,noco,ANY(greensFunction(:)%l_calc))
      !CALL greensFunction%reset(gfinp)
      IF(atoms%n_hia.GT.0 .AND. mpi%irank==0 .AND.PRESENT(hub1data)) hub1data%mag_mom = 0.0
   ENDIF

   CALL outDen%init(stars,    atoms, sphhar, vacuum, noco, input%jspins, POTDEN_TYPE_DEN)
   CALL EnergyDen%init(stars, atoms, sphhar, vacuum, noco, input%jspins, POTDEN_TYPE_EnergyDen)

   IF (mpi%irank == 0) CALL openXMLElementNoAttributes('valenceDensity')

   !In a non-collinear calcuation where the off-diagonal part of the
   !density matrix in the muffin-tins is calculated, the a- and
   !b-coef. for both spins are needed at once. Thus, cdnval is only
   !called once and both spin directions are calculated in a single run.
   results%force=0.0
   jspmax = input%jspins
   IF (noco%l_mperp.OR.banddos%l_jDOS) jspmax = 1
   DO jspin = 1,jspmax
      CALL cdnvalJob%init(mpi,input,kpts,noco,results,jspin)
      IF (sliceplot%slice) CALL cdnvalJob%select_slice(sliceplot,results,input,kpts,noco,jspin)
      CALL cdnval(eig_id,mpi,kpts,jspin,noco,nococonv,input,banddos,cell,atoms,enpara,stars,vacuum,&
                  sphhar,sym,vTot,oneD,cdnvalJob,outDen,regCharges,dos,results,moments,gfinp,&
                  hub1inp,hub1data,coreSpecInput,mcd,slab,orbcomp,jDOS,greensfImagPart)
   END DO

   IF(PRESENT(greensFunction) .AND.gfinp%n.GT.0) THEN
      IF(greensfImagPart%l_calc) THEN
         CALL greensfPostProcess(greensFunction,greensfImagPart,atoms,gfinp,input,sym,noco,mpi,&
                                 nococonv,vTot,enpara,hub1inp,hub1data,results)
      ENDIF
   ENDIF

   call val_den%copyPotDen(outDen)
   ! calculate kinetic energy density for MetaGGAs
   if(xcpot%exc_is_metagga()) then
      CALL calc_EnergyDen(eig_id, mpi, kpts, noco, nococonv,input, banddos, cell, atoms, enpara, stars,&
                             vacuum,  sphhar, sym, gfinp, hub1inp, vTot, oneD, results, EnergyDen)
   endif

   IF (mpi%irank == 0) THEN
      IF (banddos%dos.or.banddos%vacdos.or.input%cdinf) THEN
         IF (banddos%unfoldband) THEN
            eFermiPrev = 0.0
            CALL readPrevEFermi(eFermiPrev,l_error)
            CALL write_band_sc(kpts,results,eFermiPrev)
         END IF
#ifdef CPP_HDF
         CALL openBandDOSFile(banddosFile_id,input,atoms,cell,kpts,banddos)
         CALL writeBandDOSData(banddosFile_id,input,atoms,cell,kpts,results,banddos,dos,vacuum)
         CALL closeBandDOSFile(banddosFile_id)
#endif
         CALL timestart("cdngen: dos")
         CALL doswrite(eig_id,kpts,atoms,vacuum,input,banddos,sliceplot,noco,sym,cell,dos,mcd,results,&
                       slab,orbcomp,jDOS,oneD)
         IF (banddos%dos.AND.(banddos%ndir == -3)) THEN
            CALL Ek_write_sl(eig_id,kpts,atoms,vacuum,input,jspmax,sym,cell,dos,slab,orbcomp,results)
         END IF
         CALL timestop("cdngen: dos")
      END IF
   END IF

   IF ((banddos%dos.OR.banddos%vacdos).AND.(banddos%ndir/=-2)) CALL juDFT_end("DOS OK",mpi%irank)
   IF (vacuum%nstm == 3) CALL juDFT_end("VACWAVE OK",mpi%irank)

   CALL cdntot(stars,atoms,sym,vacuum,input,cell,oneD,outDen,.TRUE.,qtot,dummy,mpi,.TRUE.)
   IF (mpi%irank.EQ.0) THEN
      CALL closeXMLElement('valenceDensity')
   END IF ! mpi%irank = 0

   IF (sliceplot%slice) THEN
      IF (mpi%irank == 0) THEN
         CALL writeDensity(stars,noco,vacuum,atoms,cell,sphhar,input,sym,oneD,CDN_ARCHIVE_TYPE_CDN_const,CDN_INPUT_DEN_const,&
                           0,-1.0,0.0,.FALSE.,outDen,'cdn_slice')
      END IF
#ifdef CPP_MPI
               CALL mpi_bc_potden(mpi,stars,sphhar,atoms,input,vacuum,oneD,noco,outDen)
#endif
      CALL juDFT_end("slice OK",mpi%irank)
   END IF

   !IF (sliceplot%iplot.NE.0) THEN
   !   CALL makeplots(stars, atoms, sphhar, vacuum, input, mpi,oneD, sym, cell, noco,nococonv, outDen, PLOT_OUTDEN_Y_CORE, sliceplot)
   !END IF

   CALL timestart("cdngen: cdncore")
   if(xcpot%exc_is_MetaGGA()) then
      CALL cdncore(mpi,oneD,input,vacuum,noco,nococonv,sym,&
                   stars,cell,sphhar,atoms,vTot,outDen,moments,results, EnergyDen)
   else
      CALL cdncore(mpi,oneD,input,vacuum,noco,nococonv,sym,&
                   stars,cell,sphhar,atoms,vTot,outDen,moments,results)
   endif
   call core_den%subPotDen(outDen, val_den)
   CALL timestop("cdngen: cdncore")

   IF(.FALSE.) CALL denMultipoleExp(input, mpi, atoms, sphhar, stars, sym, cell, oneD, outDen) ! There should be a switch in the inp file for this
   IF(mpi%irank.EQ.0) THEN
      IF(input%lResMax>0) CALL resMoms(sym,input,atoms,sphhar,noco,nococonv,outDen,moments%rhoLRes) ! There should be a switch in the inp file for this
   END IF

   CALL enpara%calcOutParams(input,atoms,vacuum,regCharges)

   IF (mpi%irank == 0) CALL openXMLElementNoAttributes('allElectronCharges')
   CALL qfix(mpi,stars,atoms,sym,vacuum,sphhar,input,cell,oneD,outDen,noco%l_noco,.TRUE.,l_par=.TRUE.,force_fix=.TRUE.,fix=fix)
   IF (mpi%irank == 0) THEN
      CALL closeXMLElement('allElectronCharges')

      IF (input%jspins == 2) THEN
         noco_new = noco

         !Calculate and write out spin densities at the nucleus and magnetic moments in the spheres
         CALL magMoms(input,atoms,noco,nococonv,vTot,moments)
         if (sym%nop==1.and..not.input%film) call magMultipoles(sym,stars, atoms,cell, sphhar, vacuum, input, noco,nococonv,outden)
!         noco = noco_new

         !Generate and save the new nocoinp file if the directions of the local
         !moments are relaxed or a constraint B-field is calculated.
         IF (ANY(noco%l_relax(:atoms%ntype)).OR.noco%l_constr) THEN
          !  CALL genNewNocoInp(input,atoms,noco,noco_new)
         END IF

         IF (noco%l_soc) CALL orbMagMoms(input,atoms,noco,nococonv,moments%clmom)

      END IF
   END IF ! mpi%irank == 0
   Perform_metagga = Allocated(Energyden%Mt) &
                   .And. (Xcpot%Exc_is_metagga() .Or. Xcpot%Vx_is_metagga())
   If(Perform_metagga) Then
     CALL writeDensity(stars,noco,vacuum,atoms,cell,sphhar,input,sym,oneD,CDN_ARCHIVE_TYPE_CDN_const,CDN_INPUT_DEN_const,&
                           0,-1.0,0.0,.FALSE.,core_den,'cdnc')
   endif


#ifdef CPP_MPI
   CALL MPI_BCAST(nococonv%alph,atoms%ntype,MPI_DOUBLE_PRECISION,0,mpi%mpi_comm,ierr)
   CALL MPI_BCAST(nococonv%beta,atoms%ntype,MPI_DOUBLE_PRECISION,0,mpi%mpi_comm,ierr)
   CALL MPI_BCAST(nococonv%b_con,atoms%ntype*2,MPI_DOUBLE_PRECISION,0,mpi%mpi_comm,ierr)
   CALL MPI_BCAST(nococonv%qss,3,MPI_DOUBLE_PRECISION,0,mpi%mpi_comm,ierr)

   CALL mpi_bc_potden(mpi,stars,sphhar,atoms,input,vacuum,oneD,noco,outDen)
#endif

END SUBROUTINE cdngen

END MODULE m_cdngen
